plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
}

ext {
    hytaleHome = "C:\\Users\\alber\\AppData\\Roaming\\Hytale"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

// Quiet warnings about missing Javadocs.
javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}
repositories {
    mavenCentral()
    maven { url 'https://dl.cloudsmith.io/public/lio/neotale/maven/' }
}

// Adds the Hytale server as a build dependency, allowing you to reference and
// compile against their code. This requires you to have Hytale installed using
// the official launcher for now.
dependencies {
    implementation(files("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar"))
//    implementation "net.liopyu:neotale:${neoTaleVersion}"

    compileOnly("org.projectlombok:lombok:1.18.42")
    annotationProcessor("org.projectlombok:lombok:1.18.42")
}

// Create the working directory to run the server if it does not already exist.
def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

// Updates the manifest.json file with the latest properties defined in the
// build.properties file. Currently we update the version and if packs are
// included with the plugin.
tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    doLast {
        if (!manifestFile.exists()) {
            throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
        }
        def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
        manifestJson.Version = version
        manifestJson.IncludesAssetPack = includes_pack.toBoolean()
        manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
}

def modsRunDir = file("$serverRunDir/mods")

tasks.register('prepareRunMods', Sync) {
    dependsOn 'jar'
    into modsRunDir
    from(tasks.named('jar').flatMap { it.archiveFile })
    from(configurations.runtimeClasspath) {
        include '**/*.jar'
        exclude 'HytaleServer.jar'
    }
}

tasks.named('processResources') {
    dependsOn 'updatePluginManifest'
}

def pluginDirForModsArg = sourceSets.main.java.srcDirs.first().parentFile.absolutePath
def assetsZipPath = file("$hytaleHome/install/$patchline/package/game/latest/Assets.zip").absolutePath
def serverJarPath = file("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar").absolutePath
idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = "--allow-op --assets=\"$hytaleHome/install/$patchline/package/game/latest/Assets.zip\""
        if (includes_pack.toBoolean()) {
            programParameters += " --mods=${sourceSets.main.java.srcDirs.first().parentFile.absolutePath}"
        }
        if (load_user_mods.toBoolean()) {
            programParameters += " --mods=$hytaleHome/UserData/Mods"
        }
        workingDirectory = serverRunDir.absolutePath
    }
}
//idea.project.settings.runConfigurations {
//    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
//        mainClass = 'com.hypixel.hytale.Main'
//        moduleName = project.idea.module.name + '.main'
//        jvmArgs = "-Xms4G -Xmx10G"
//
//        programParameters = "--allow-op --assets=\"${assetsZipPath}\" --mods=\"${modsRunDir.absolutePath}\""
//
//        if (load_user_mods.toBoolean()) {
//            programParameters += " --mods=\"$hytaleHome/UserData/Mods\""
//        }
//
//        workingDirectory = serverRunDir.absolutePath
//    }
//}
tasks.register('runServer', JavaExec) {
    group = 'hytale'
    description = 'Runs the Hytale server with this plugin and default assets.'

    dependsOn 'prepareRunMods'

    classpath = files(serverJarPath)
    mainClass = 'com.hypixel.hytale.Main'
    workingDir = serverRunDir
    jvmArgs "-Xms4G", "-Xmx10G"

    args '--allow-op', "--assets=${assetsZipPath}"


    if (load_user_mods.toBoolean()) {
        args "--mods=$hytaleHome/UserData/Mods"
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
